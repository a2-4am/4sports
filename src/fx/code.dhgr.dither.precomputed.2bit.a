!cpu 6502
!to "build/FXCODE/DHGRDITHER2",plain
*=$6200

         !source "src/fx/macros.a"

;DHGRDitherPrecomputed2Bit
         jsr   iBuildDHGRDitherMasks
         jsr   iBuildDHGRSparseBitmasks2Bit
         jsr   iBuildHGRTables
         jsr   iBuildDHGRMirrorCols

         ; phase 1 - in reverse, with additional masking (dithering)
         jsr   .copytozp

         ; set up starting coordinate for reading coordinates in reverse order
         +LDADDR EndCoordinates2Bit-2
         sta   <.input
         sty   <.input+1

         ; set up EOF marker to stop reading coordinates in reverse order
         lda   #$00
         sta   Coordinates2Bit-2

         ; set up logic to advance to next coordinates in reverse order
         ldx   #(.next2_end-.next2_start-1)
-        lda   .next2_start, x
         sta   <.next, x
         dex
         bpl   -

         jsr   .InputLoop

         bit   KBD
         bmi   .start

         ; phase 2 - in order, without additional masking
         jsr   .copytozp

         ; redirect additional masking pointers to an array that contains #$FFs (so no dithering)
         lda   #<no_masks
         sta   <.evenrow_ptr
         sta   <.oddrow_ptr

         jmp   .InputLoop

.copytozp
         +COPY_TO_0 .start, .end
.start
!pseudopc 0 {
.Exit2Bit rts
.evenrow_ptr
         !word evenrow_masks
.oddrow_ptr
         !word oddrow_masks
.InputLoop
         ldy   #0
.input=*+1
         ldx   Coordinates2Bit       ; first value: HGR row + 1
         beq   .Exit2Bit             ; if 0 then we're done
         +ROW_X_TO_2BIT_BASE_ADDRESSES .src1, .src2, .dest1, .dest2

         iny
         lda   (<.input), y
         +HIGH_3_LOW_5 .input

         ; main 2x2 block in left half
         clc
-        lda   copymasks2bit, x
         beq   +
.src1=*+1
         lda   $FDFD, y
         eor   (<.dest1), y
         and   copymasks2bit, x
         and   (<.evenrow_ptr), y
         eor   (<.dest1), y
.dest1=*+1
         sta   $FDFD, y
.src2=*+1
         lda   $FDFD, y
         eor   (<.dest2), y
         and   copymasks2bit, x
         and   (<.oddrow_ptr), y
         eor   (<.dest2), y
.dest2=*+1
         sta   $FDFD, y
+        bcs   +
         sta   READAUXMEM
         sta   WRITEAUXMEM
         sec
         bcs   -
+        sta   READMAINMEM
         sta   WRITEMAINMEM

         ; corresponding 2x2 block in right half (same row, opposite column)
         lda   mirror_cols, y
         tay
         clc
-        lda   mirror_copymasks2bit, x
         beq   +
         +COPY_BIT_DITHER .src1, .dest1, mirror_copymasks2bit, .evenrow_ptr
         +COPY_BIT_DITHER .src2, .dest2, mirror_copymasks2bit, .oddrow_ptr
+        bcs   +
         sta   READAUXMEM
         sta   WRITEAUXMEM
         sec
         bcs   -
+        sta   READMAINMEM
         sta   WRITEMAINMEM

.next
         inc   <.input
         inc   <.input
         bne   +
         bit   KBD
         bmi   ++
         inc   <.input+1
+        jmp   .InputLoop
++       rts

}
.end

.next2_start
!pseudopc .next {
         lda   <.input
         php
         dec   <.input
         dec   <.input
         plp
         bne   +
         dec   <.input+1
         bit   KBD
         bmi   ++
+        jmp   .InputLoop
++       rts
}
.next2_end
