;license:MIT
;(c) 2020, 2023 by qkumba

!cpu 6502
!to "build/PRELAUNCH.INDEXED/ONE.ON.ONE",plain
*=$106

         !source "src/macros.a"

         jsr   HideLaunchArtwork
         +READ_RAM2_WRITE_RAM2
         jsr   EnableAccelerator
         lda   MockingboardStuff
         cmp   #1
         and   #7
         ora   #$C0
         php
         pha
         bcc   +
         +LC_REBOOT
+
         +READ_ROM_NO_WRITE
         lda   #$60
         sta   $408B
         jsr   $4000      ; decompress
         pla
         plp
         php
         bcc   skipmb
         cmp   #$C4
         beq   skipmb1

         sta   $E9BC      ; Replace #$C4 with configured slot
         ldy   #00
-        ldx   $EAF6,Y    ; Find #$C4s
         cpx   #$C4
         bne   +
         sta   $EAF6,Y    ; Replace #$C4 with configured slot
+        iny
         bne   -

skipmb1
         plp
         cli              ; enable MB music on title
         php

skipmb
         +READ_RAM2_WRITE_RAM2
         jsr   DisableAccelerator
         plp
         bcs   +
         +READ_ROM_NO_WRITE
+
         jmp   $400

!if * > HideLaunchArtwork {
  !error "code is too large, ends at ", *
}
